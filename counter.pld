GAL20V8 ; The device type
counter ; The signature

; Pin definitions:
; 1st line: Pins 1 - 12
; 2nd line: Pins 13 - 24

Clock  Clear NC  NC  NC  NC  NC  NC  NC  NC  NC  GND
/OE    NC    Q0  Q1  Q2  Q3  Q4  Q5  Q6  Q7  NC  VCC

; Logic equations

Q0.R = /Clear * /Q0


Q1.R =   /Clear *  Q1 * /Q0
       + /Clear * /Q1 *  Q0


Q2.R =   /Clear *  Q2 * /Q1
       + /Clear *  Q2 * /Q0
       + /Clear * /Q2 *  Q1 *  Q0


Q3.R =  /Clear  *  Q3 * /Q2
       + /Clear *  Q3 * /Q1
       + /Clear *  Q3 * /Q0
       + /Clear * /Q3 *  Q2 *  Q1 *  Q0

Q4.R =   /Clear *  Q4 * /Q3
       + /Clear *  Q4 * /Q2
       + /Clear *  Q4 * /Q1
       + /Clear *  Q4 * /Q0
       + /Clear * /Q4 *  Q3 * Q2 *  Q1 *  Q0

Q5.R =   /Clear *  Q5 * /Q4
       + /Clear *  Q5 * /Q3
       + /Clear *  Q5 * /Q2
       + /Clear *  Q5 * /Q1
       + /Clear *  Q5 * /Q0
       + /Clear * /Q5 *  Q4 * Q3 * Q2 *  Q1 *  Q0

Q6.R =   /Clear *  Q6 * /Q5
       + /Clear *  Q6 * /Q4
       + /Clear *  Q6 * /Q3
       + /Clear *  Q6 * /Q2
       + /Clear *  Q6 * /Q1
       + /Clear *  Q6 * /Q0
       + /Clear * /Q6 *  Q5 * Q4 * Q3 * Q2 *  Q1 *  Q0

Q7.R =   /Clear *  Q7 * /Q6
       + /Clear *  Q7 * /Q5
       + /Clear *  Q7 * /Q4
       + /Clear *  Q7 * /Q3
       + /Clear *  Q7 * /Q2
       + /Clear *  Q7 * /Q1
       + /Clear *  Q7 * /Q0
       + /Clear * /Q7 *  Q6 * Q5 * Q4 * Q3 * Q2 *  Q1 *  Q0

DESCRIPTION:
A self incrementing 8-bit counter. Clear is used to reset.

